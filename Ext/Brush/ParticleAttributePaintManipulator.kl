

/*
 *  Copyright 2010-2015 Fabric Software Inc. All rights reserved.
 */



require Singletons;
require InlineDrawing;


//////////////////////////////////////
// ParticleAttributePaintManipulator_PaintData

/**
\versionadded 1.12.0

The :kl-ref:`ParticleAttributePaintManipulator_PaintData` defines the data collected
during painting. :kl-ref:`ParticleAttributePaintManipulator_PaintData` objects are generated
with each invocation of :kl-ref:`ParticleAttributePaintManipulator.onPaint`

\seealso ParticleAttributePaintManipulator, UndoRedoCommand
*/
struct ParticleAttributePaintManipulator_PaintData {
  Points points;
  ScalarAttribute attribute;
  Integer pointIds[];
  /// The previous values of the ScalarAttribute before this 
  Scalar oldValues[];
  Scalar newValues[];
};

function ParticleAttributePaintManipulator_PaintData(Points points, ScalarAttribute attribute){
  this.points = points;
  this.attribute = attribute;
}

// Merge the new patin data into the existing paint data to create one set that can be undone/redone
function ParticleAttributePaintManipulator_PaintData.mergeIn!(ParticleAttributePaintManipulator_PaintData paintData){
  for (Integer i = 0; i < paintData.pointIds.size(); i++) {
    Integer id = -1;
    for (Integer j = 0; j < this.pointIds.size(); j++) {
      if(paintData.pointIds[i] == this.pointIds[j]){
        id = j;
        break;
      }
    }
    if(id != -1){
      this.newValues[id] = paintData.newValues[i];
    }
    else{
      this.pointIds.push(paintData.pointIds[i]);
      this.oldValues.push(paintData.oldValues[i]);
      this.newValues.push(paintData.newValues[i]);
    }
  }
}

function ParticleAttributePaintManipulator_PaintData.doAction!(){
  Ref<ScalarAttribute> attrRef = this.attribute;
  for (Integer i = 0; i < this.pointIds.size(); i++) {
    attrRef.values[ this.pointIds[i] ] = this.newValues[ i ];
  }
  attrRef.incrementVersion();
}


function ParticleAttributePaintManipulator_PaintData.undoAction!(){
  Ref<ScalarAttribute> attrRef = this.attribute;
  for (Integer i = 0; i < this.pointIds.size(); i++) {
    attrRef.values[ this.pointIds[i] ] = this.oldValues[ i ];
  }
  attrRef.incrementVersion();
}

//////////////////////////////////////

/**
\versionadded 1.12.0
\internal

The :kl-ref:`ParticleAttributePaintCommand` contains a dictionary of
:kl-ref:`ParticleAttributePaintManipulator_PaintData` object. One for
each geometry. As a paint stoke moves from one geometry to another
the paint data is added to the relevant container in the 
:kl-ref:`ParticleAttributePaintCommand`.

\seealso ParticleAttributePaintManipulator, UndoRedoCommand
*/
object ParticleAttributePaintCommand : UndoRedoCommand {
  /// a dictionary of :kl-ref:`ParticleAttributePaintManipulator_PaintData` object. 
  /// One for each geometry. As a paint stoke moves from one geometry to another
  /// the paint data is added to the relevant container in the 
  /// :kl-ref:`ParticleAttributePaintCommand`.
  ParticleAttributePaintManipulator_PaintData paintData[Points];
};

function ParticleAttributePaintCommand(){
}

function String ParticleAttributePaintCommand.getDesc(){
  String desc;
  for(points in this.paintData){
    desc += this.paintData[points].attribute.getName();
    break;
  }
  return "Paint Surface attribute:" + desc;
}

function ParticleAttributePaintCommand.doAction!(){
  for(points in this.paintData){
    this.paintData[points].doAction();
  }
}

function ParticleAttributePaintCommand.undoAction!(){
  for(points in this.paintData){
    this.paintData[points].undoAction();
  }
}

/// Merges the current stroke data with the existing data collected over the
/// course of the paint stroke. 
/// \param paintData the data collected during the current event.
function ParticleAttributePaintCommand.addPaintData!(ParticleAttributePaintManipulator_PaintData paintData){
  if(this.paintData.has(paintData.points)){
    this.paintData[paintData.points].mergeIn(paintData); 
  }
  else{
    this.paintData[paintData.points] = paintData;
  }
}


///////////////////////////////////////////////
// ParticleAttributePaintManipulator

/**
\versionadded 1.12.0

The :kl-ref:`ParticleAttributePaintManipulator` enables the painting of a scalar 
vertex attribute on a :kl-ref:`Points`. These scalar attributes can be used 
to control the effects of custom deformers and the ParticleAttributePaintManipulator 
can provide an artist workflow for defining these weightmaps. 

\rst
 Usage
 =====
 - Click and drag with the Left mouse button to paint a paint stroke
 - Click and drag with the Right mouse button to erase existing paint data.
 - Click and drag with the Middle mouse button to smooth existing paint data.
 - The mouse wheel can be used to resize the paint brush, except on platforms that do not support the mouse wheel.
 - The 'Ctrl' modifier key can be held during a left button mouse drag to resize the brush.
 - The 'Alt' modifier key can be held during a left button mouse drag to erase paint data.
 - The 'Shift' modifier key can be held during a left button mouse drag to smooth paint data.

\endrst

\seealso ParticlePaintManipulator
*/
object ParticleAttributePaintManipulator : ParticlePaintManipulator {
  String attributeName;

  Integer paintMode;
  ParticleAttributePaintCommand command;
};


function ParticleAttributePaintManipulator(){
  this.init();
  this.attributeName = "masses";
}

function ParticleAttributePaintManipulator.setAttributeName!(String attributeName){
  this.attributeName = attributeName;
}

function ParticleAttributePaintManipulator.addTargetGeometry!(InlineInstance instance){
  InlineShape shape = instance.getShape();
  Points points = shape.getGeometry();
  if(points == null){
    report("WARNING: InlineInstance's geometry is not a Points:" + instance.getName() );
    return;
  }
  // Ensure that the attribute is added to the points.
  Ref<GeometryAttributes> attrs = points.getAttributes();
  attrs.getOrCreateAttribute(this.attributeName, ScalarAttribute);

  this.parent.addTargetGeometry(instance);
}

/// Called when the stroke starts when the user presses down the mouse button
/// Adds the generated :kl-ref:`ParticleAttributePaintCommand` command to the Host undo stack.
function ParticleAttributePaintManipulator.onStrokeBegin!(io MouseEvent mouseEvent){

  // On MouseMove/Mousedown.
  if(mouseEvent.button == MouseButton_LeftButton){
    // Some DCC apps are not giving us Middle and Right mouse buttons. 
    // Use modifier keys to distinguish the action.
    if((mouseEvent.modifiers & ModiferKey_Alt) != 0)
      this.paintMode = 1; // Erase
    else if((mouseEvent.modifiers & ModiferKey_Shift) != 0)
      this.paintMode = 2; // Smooth
    else
      this.paintMode = 0; // Paint
  }
  else if(mouseEvent.button == MouseButton_RightButton)
    this.paintMode = 1; // Erase
  else if(mouseEvent.button == MouseButton_MiddleButton)
    this.paintMode = 2; // Smooth

  this.command = ParticleAttributePaintCommand();
  mouseEvent.accept();
}

/// Called when the stroke ends when the user releases the mouse button
/// Adds the generated command to the Host undo stack.
function ParticleAttributePaintManipulator.onStrokeEnd!(io MouseEvent mouseEvent){
  // Register a command only if painting actually occured.
  // There should be at least one geometry added to the paint data. 
  if(this.command.paintData.size() > 0)
    mouseEvent.getHost().addUndoRedoCommand(this.command);
}


/// \internal
operator ParticleSculptManipulator_paint<<<index>>>(
                        ParticlePaintManipulator_PointCollection collectedPoints,
                        Ref<ScalarAttribute> weightMapAttr,
                        io ParticleAttributePaintManipulator_PaintData paintData
){
  Scalar  weight    = collectedPoints.weights[index];
  Integer pointId   = collectedPoints.pointIds[index];
  Scalar  oldScalar = weightMapAttr.values[pointId];
  Scalar  newScalar = Math_linearInterpolate(oldScalar, Scalar(1.0), weight);

  paintData.oldValues[index] = oldScalar;
  paintData.newValues[index] = newScalar;
}


/// \internal
operator ParticleSculptManipulator_erase<<<index>>>(
  ParticlePaintManipulator_PointCollection collectedPoints,
  Ref<ScalarAttribute> weightMapAttr,
  io ParticleAttributePaintManipulator_PaintData paintData
 ) {
    Scalar weight = collectedPoints.weights[index];
    Integer pointId = collectedPoints.pointIds[index];
    Scalar oldScalar = weightMapAttr.values[pointId];
    Scalar newScalar = Math_linearInterpolate(oldScalar, Scalar(0.0), weight);

    paintData.oldValues[index] = oldScalar;
    paintData.newValues[index] = newScalar;
}


/// Smooth Points
/// \internal
operator ParticleAttributePaintManipulator_smoothAttributeData<<<index>>>(
  Scalar weightMapValues[],
  ParticlePaintManipulator_PointCollection collectedPoints,
  io Scalar results[]
 ) {

  //Pseudo-gaussian: center weight = 0.5, neighbor weights sum = 0.5
  Index pointId = collectedPoints.pointIds[index];
  Scalar weight = collectedPoints.weights[index];
  Scalar value = weightMapValues[pointId];

  /* FIXME: implement later
  LocalL16UInt32Array surroundingPoints;
  Vec3 p = collectedPoints.points.getPosition( pointId );
  ParticleAttributePaintManipulator_GetPointDistance<<< ( collectedPoints.points, p, surroundingPoints );
  //collectedPoints.points.getPointSurroundingPoints(pointId, false, surroundingPoints);
  UInt32 nbNei = surroundingPoints.size();
  
  if( nbNei ) {
    Scalar neiSum = 0.0;
    for( UInt32 i = 0; i < nbNei; ++i ) {
      UInt32 neiPt = surroundingPoints.get(i);
      neiSum += weightMapValues[neiPt];
    }
    neiSum /= Scalar(nbNei);
    value = Math_linearInterpolate(value, (value + neiSum) * 0.5, weight);
  }
  */
  results[index] = value;
}

/// Called during the paint stroke for each mouse move event.
function ParticleAttributePaintManipulator.onPaint!(io MouseEvent mouseEvent, ParticlePaintManipulator_PointCollection collectedPoints){

  Ref<GeometryAttributes> attrs = collectedPoints.points.getAttributes();
  Ref<ScalarAttribute> weightMapAttr = attrs.getAttribute(this.attributeName);

  if(weightMapAttr == null){
    report("ERROR: Geometry Does not have the attribute:" + this.attributeName );
    return;
  }

  UInt32 nbPoints = collectedPoints.pointIds.size();

  ParticleAttributePaintManipulator_PaintData paintData(collectedPoints.points, weightMapAttr);
  paintData.pointIds = collectedPoints.pointIds;
  paintData.oldValues.resize(collectedPoints.pointIds.size);
  paintData.newValues.resize(collectedPoints.pointIds.size);

  switch(this.paintMode){
  case 0: // Paint
    ParticleSculptManipulator_paint<<<nbPoints>>>(
      collectedPoints,
      weightMapAttr,
      paintData
    );
    break;
  case 1: // Erase
    ParticleSculptManipulator_erase<<<nbPoints>>>(
      collectedPoints,
      weightMapAttr,
      paintData
    );
    break;
  case 2: // Smooth
    Scalar smoothedValues[];
    smoothedValues.resize(nbPoints);
    ParticleAttributePaintManipulator_smoothAttributeData<<<nbPoints>>>(
      weightMapAttr.values,
      collectedPoints,
      smoothedValues
    );
    for(Integer i = 0; i < nbPoints; ++i ) {
      paintData.oldValues[i] = weightMapAttr.values[ collectedPoints.pointIds[i] ];
      paintData.newValues[i] = smoothedValues[i];
    }
    break;
  }
  // do the command immedietly...
  paintData.doAction();
  // Merge this paintData with the command so that we only generate one undo command 
  // for the entire paint stroke, even if we cross multipel geometries. 
  this.command.addPaintData(paintData);
}
